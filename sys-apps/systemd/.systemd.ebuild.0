# Copyright 2011-2020 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=7

# CLIP OS: We use cros-workon instead
#if [[ ${PV} == 9999 ]]; then
#	EGIT_REPO_URI="https://github.com/systemd/systemd.git"
#	inherit git-r3
#else
#	if [[ ${PV} == *.* ]]; then
#		MY_PN=systemd-stable
#	else
#		MY_PN=systemd
#	fi
#	MY_PV=${PV/_/-}
#	MY_P=${MY_PN}-${MY_PV}
#	S=${WORKDIR}/${MY_P}
#	SRC_URI="https://github.com/systemd/${MY_PN}/archive/v${MY_PV}/${MY_P}.tar.gz"
#	KEYWORDS="~alpha ~amd64 ~arm ~arm64 ~hppa ~ia64 ~mips ~ppc ~ppc64 ~sparc ~x86"
#fi

CROS_WORKON_PROJECT='external/systemd'
CROS_WORKON_COMMIT="8aa93ae0c47c5d90df4439ade26b63af665d90e3" # v245.7
KEYWORDS="amd64"

PYTHON_COMPAT=( python3_{6,7,8} )

# cros-workon must be imported first, in cases where cros-workon and
# another eclass exports the same function (say src_compile) we want
# the later eclass's version to win. Only need src_unpack from workon.
inherit cros-workon

inherit bash-completion-r1 linux-info meson multilib-minimal ninja-utils pam python-any-r1 systemd toolchain-funcs udev usr-ldscript

DESCRIPTION="System and service manager for Linux"
HOMEPAGE="https://www.freedesktop.org/wiki/Software/systemd"

LICENSE="GPL-2 LGPL-2.1 MIT public-domain"
SLOT="0/2"
IUSE="acl apparmor audit build cgroup-hybrid cryptsetup curl dns-over-tls elfutils +gcrypt gnuefi homed http +hwdb idn importd +kmod +lz4 lzma nat pam pcre pkcs11 policykit pwquality qrcode repart +resolvconf +seccomp selinux split-usr static-libs +sysv-utils test vanilla xkb"
# CLIP OS specific use flags
IUSE+=" clipos_use_sysusers clipos_use_networkd"
IUSE+=" clipos_instrumentations_coredump-handler clipos_instrumentations_early-root-shell"

REQUIRED_USE="
	homed? ( cryptsetup )
	importd? ( curl gcrypt lzma )
"
RESTRICT="!test? ( test )"

MINKV="3.11"

OPENSSL_DEP=">=dev-libs/openssl-1.1.0:0="

COMMON_DEPEND=">=sys-apps/util-linux-2.30:0=[${MULTILIB_USEDEP}]
	sys-libs/libcap:0=[${MULTILIB_USEDEP}]
	acl? ( sys-apps/acl:0= )
	apparmor? ( sys-libs/libapparmor:0= )
	audit? ( >=sys-process/audit-2:0= )
	cryptsetup? ( >=sys-fs/cryptsetup-2.0.1:0= )
	curl? ( net-misc/curl:0= )
	dns-over-tls? ( >=net-libs/gnutls-3.6.0:0= )
	elfutils? ( >=dev-libs/elfutils-0.158:0= )
	gcrypt? ( >=dev-libs/libgcrypt-1.4.5:0=[${MULTILIB_USEDEP}] )
	homed? ( ${OPENSSL_DEP} )
	http? (
		>=net-libs/libmicrohttpd-0.9.33:0=[epoll(+)]
		>=net-libs/gnutls-3.1.4:0=
	)
	idn? ( net-dns/libidn2:= )
	importd? (
		app-arch/bzip2:0=
		sys-libs/zlib:0=
	)
	kmod? ( >=sys-apps/kmod-15:0= )
	lz4? ( >=app-arch/lz4-0_p131:0=[${MULTILIB_USEDEP}] )
	lzma? ( >=app-arch/xz-utils-5.0.5-r1:0=[${MULTILIB_USEDEP}] )
	nat? ( net-firewall/iptables:0= )
	pam? ( sys-libs/pam:=[${MULTILIB_USEDEP}] )
	pkcs11? ( app-crypt/p11-kit:0= )
	pcre? ( dev-libs/libpcre2 )
	pwquality? ( dev-libs/libpwquality:0= )
	qrcode? ( media-gfx/qrencode:0= )
	repart? ( ${OPENSSL_DEP} )
	seccomp? ( >=sys-libs/libseccomp-2.3.3:0= )
	selinux? ( sys-libs/libselinux:0= )
	xkb? ( >=x11-libs/libxkbcommon-0.4.1:0= )"

# Newer linux-headers needed by ia64, bug #480218
DEPEND="${COMMON_DEPEND}
	>=sys-kernel/linux-headers-${MINKV}
	gnuefi? ( >=sys-boot/gnu-efi-3.0.2 )
"

# baselayout-2.2 has /run
RDEPEND="${COMMON_DEPEND}
	acct-group/adm
	acct-group/wheel
	acct-group/kmem
	acct-group/tty
	acct-group/utmp
	acct-group/audio
	acct-group/cdrom
	acct-group/dialout
	acct-group/disk
	acct-group/input
	acct-group/kvm
	acct-group/render
	acct-group/tape
	acct-group/video
	acct-group/systemd-journal
	acct-user/systemd-journal-remote
	acct-user/systemd-coredump
	acct-user/systemd-network
	acct-user/systemd-resolve
	acct-user/systemd-timesync
	>=sys-apps/baselayout-2.2
	selinux? ( sec-policy/selinux-base-policy[systemd] )
	sysv-utils? ( !sys-apps/sysvinit )
	!sysv-utils? ( sys-apps/sysvinit )
	resolvconf? ( !net-dns/openresolv )
	!build? ( || (
		sys-apps/util-linux[kill(-)]
		sys-process/procps[kill(+)]
		sys-apps/coreutils[kill(-)]
	) )
	!sys-auth/nss-myhostname
	!sys-fs/eudev
	!sys-fs/udev
"

# sys-apps/dbus: the daemon only (+ build-time lib dep for tests)
PDEPEND=">=sys-apps/dbus-1.9.8[systemd]
	hwdb? ( >=sys-apps/hwids-20150417[udev] )
	>=sys-fs/udev-init-scripts-25
	policykit? ( sys-auth/polkit )
	!vanilla? ( sys-apps/gentoo-systemd-integration )"

BDEPEND="
	app-arch/xz-utils:0
	dev-util/gperf
	>=dev-util/meson-0.46
	>=dev-util/intltool-0.50
	>=sys-apps/coreutils-8.16
	sys-devel/m4
	virtual/pkgconfig
	test? ( sys-apps/dbus )
	app-text/docbook-xml-dtd:4.2
	app-text/docbook-xml-dtd:4.5
	app-text/docbook-xsl-stylesheets
	dev-libs/libxslt:0
	$(python_gen_any_dep 'dev-python/lxml[${PYTHON_USEDEP}]')
"

python_check_deps() {
	has_version -b "dev-python/lxml[${PYTHON_USEDEP}]"
}

pkg_pretend() {
	if [[ ${MERGE_TYPE} != buildonly ]]; then
		if use test && has pid-sandbox ${FEATURES}; then
			ewarn "Tests are known to fail with PID sandboxing enabled."
			ewarn "See https://bugs.gentoo.org/674458."
		fi

		local CONFIG_CHECK="~AUTOFS4_FS ~BLK_DEV_BSG ~CGROUPS
			~CHECKPOINT_RESTORE ~DEVTMPFS ~EPOLL ~FANOTIFY ~FHANDLE
			~INOTIFY_USER ~IPV6 ~NET ~NET_NS ~PROC_FS ~SIGNALFD ~SYSFS
			~TIMERFD ~TMPFS_XATTR ~UNIX
			~CRYPTO_HMAC ~CRYPTO_SHA256 ~CRYPTO_USER_API_HASH
			~!GRKERNSEC_PROC ~!IDE ~!SYSFS_DEPRECATED
			~!SYSFS_DEPRECATED_V2"

		use acl && CONFIG_CHECK+=" ~TMPFS_POSIX_ACL"
		use seccomp && CONFIG_CHECK+=" ~SECCOMP ~SECCOMP_FILTER"
		kernel_is -lt 3 7 && CONFIG_CHECK+=" ~HOTPLUG"
		kernel_is -lt 4 7 && CONFIG_CHECK+=" ~DEVPTS_MULTIPLE_INSTANCES"
		kernel_is -ge 4 10 && CONFIG_CHECK+=" ~CGROUP_BPF"

		if linux_config_exists; then
			local uevent_helper_path=$(linux_chkconfig_string UEVENT_HELPER_PATH)
			if [[ -n ${uevent_helper_path} ]] && [[ ${uevent_helper_path} != '""' ]]; then
				ewarn "It's recommended to set an empty value to the following kernel config option:"
				ewarn "CONFIG_UEVENT_HELPER_PATH=${uevent_helper_path}"
			fi
			if linux_chkconfig_present X86; then
				CONFIG_CHECK+=" ~DMIID"
			fi
		fi

		if kernel_is -lt ${MINKV//./ }; then
			ewarn "Kernel version at least ${MINKV} required"
		fi

		check_extra_config
	fi
}

pkg_setup() {
	:
}

src_unpack() {
	default
	# CLIP OS: We use cros-workon instead
	#[[ ${PV} != 9999 ]] || git-r3_src_unpack
	cros-workon_src_unpack
}

src_prepare() {
	# CLIP OS: Do not use Gentoo patches as we track systemd-stable
	default
	return 0

	# Do NOT add patches here
	local PATCHES=()

	[[ -d "${WORKDIR}"/patches ]] && PATCHES+=( "${WORKDIR}"/patches )

	# Add local patches here
	PATCHES+=(
		"${FILESDIR}/245-libmicrohttpd-0.9.71.patch"
	)

	if ! use vanilla; then
		PATCHES+=(
			"${FILESDIR}/gentoo-Dont-enable-audit-by-default.patch"
			"${FILESDIR}/gentoo-systemd-user-pam.patch"
			"${FILESDIR}/gentoo-generator-path-r1.patch"
			"${FILESDIR}/gentoo-systemctl-disable-sysv-sync.patch"
		)
	fi

	default
}

src_configure() {
	# Prevent conflicts with i686 cross toolchain, bug 559726
	tc-export AR CC NM OBJCOPY RANLIB

	python_setup

	multilib-minimal_src_configure
}

meson_use() {
	usex "$1" true false
}

meson_multilib() {
	if multilib_is_native_abi; then
		echo true
	else
		echo false
	fi
}

meson_multilib_native_use() {
	if multilib_is_native_abi && use "$1"; then
		echo true
	else
		echo false
	fi
}

multilib_src_configure() {
	local myconf=(
		--localstatedir="${EPREFIX}/var"
		# CLIP OS: Update support URL
		#-Dsupport-url="https://gentoo.org/support/"
		-Dsupport-url="https://clip-os.org/contact/"
		-Dpamlibdir="$(getpam_mod_dir)"
		# avoid bash-completion dep
		-Dbashcompletiondir="$(get_bashcompdir)"
		# make sure we get /bin:/sbin in PATH
		-Dsplit-usr=$(usex split-usr true false)
		-Dsplit-bin=true
		-Drootprefix="$(usex split-usr "${EPREFIX:-/}" "${EPREFIX}/usr")"
		-Drootlibdir="${EPREFIX}/usr/$(get_libdir)"
		# Avoid infinite exec recursion, bug 642724
		-Dtelinit-path="${EPREFIX}/lib/sysvinit/telinit"
		# CLIP OS: Enabled only for efiboot
		-Defi=$(meson_multilib_native_use gnuefi)
		# CLIP OS: Unused
		# no deps
		#-Dima=true
		-Dima=false
		# CLIP OS: Only use cgroups v2 with a unified hierarchy
		# -Ddefault-hierarchy=$(usex cgroup-hybrid hybrid unified)
		-Ddefault-hierarchy=unified
		# Optional components/dependencies
		-Dacl=$(meson_multilib_native_use acl)
		-Dapparmor=$(meson_multilib_native_use apparmor)
		-Daudit=$(meson_multilib_native_use audit)
		-Dlibcryptsetup=$(meson_multilib_native_use cryptsetup)
		-Dlibcurl=$(meson_multilib_native_use curl)
		-Ddns-over-tls=$(meson_multilib_native_use dns-over-tls)
		-Delfutils=$(meson_multilib_native_use elfutils)
		-Dgcrypt=$(meson_use gcrypt)
		-Dgnu-efi=$(meson_multilib_native_use gnuefi)
		-Defi-libdir="${ESYSROOT}/usr/$(get_libdir)"
		-Dhomed=$(meson_multilib_native_use homed)
		-Dhwdb=$(meson_multilib_native_use hwdb)
		-Dmicrohttpd=$(meson_multilib_native_use http)
		-Didn=$(meson_multilib_native_use idn)
		-Dimportd=$(meson_multilib_native_use importd)
		-Dbzip2=$(meson_multilib_native_use importd)
		-Dzlib=$(meson_multilib_native_use importd)
		-Dkmod=$(meson_multilib_native_use kmod)
		-Dlz4=$(meson_use lz4)
		-Dxz=$(meson_use lzma)
		-Dlibiptc=$(meson_multilib_native_use nat)
		-Dpam=$(meson_use pam)
		-Dp11kit=$(meson_multilib_native_use pkcs11)
		-Dpcre2=$(meson_multilib_native_use pcre)
		-Dpolkit=$(meson_multilib_native_use policykit)
		-Dpwquality=$(meson_multilib_native_use pwquality)
		-Dqrencode=$(meson_multilib_native_use qrcode)
		-Drepart=$(meson_multilib_native_use repart)
		-Dseccomp=$(meson_multilib_native_use seccomp)
		-Dselinux=$(meson_multilib_native_use selinux)
		# CLIP OS: Unused
		-Dsmack=false
		-Ddbus=$(meson_multilib_native_use test)
		-Dxkbcommon=$(meson_multilib_native_use xkb)
		-Dntp-servers="0.gentoo.pool.ntp.org 1.gentoo.pool.ntp.org 2.gentoo.pool.ntp.org 3.gentoo.pool.ntp.org"
		# Breaks screen, tmux, etc.
		# CLIP OS: Intended behaviour (no persistence beyond session lifetime)
		#-Ddefault-kill-user-processes=false
		-Ddefault-kill-user-processes=true
		-Dcreate-log-dirs=false

		# multilib options
		-Dbacklight=$(meson_multilib)
		# CLIP OS: Disabled binfmt support
		#-Dbinfmt=$(meson_multilib)
		-Dbinfmt=false
		# CLIP OS: Install systemd-coredump handler only for debug
		#-Dcoredump=$(meson_multilib)
		-Dcoredump=$(meson_multilib_native_use clipos_instrumentations_coredump-handler)
		-Denvironment-d=$(meson_multilib)
		-Dfirstboot=$(meson_multilib)
		# CLIP OS: Hibernation not supported for now (ephemeral swap)
		#-Dhibernate=$(meson_multilib)
		-Dhibernate=false
		# CLIP OS: Disable hostnamed support
		#-Dhostnamed=$(meson_multilib)
		-Dhostnamed=false
		-Dhwdb=$(meson_multilib)
		-Dldconfig=$(meson_multilib)
		# CLIP OS: Disable localed support
		#-Dlocaled=$(meson_multilib)
		-Dlocaled=false
		-Dman=$(meson_multilib)
		# CLIP OS: Temporarily enabled as system network manager
		#-Dnetworkd=$(meson_multilib)
		-Dnetworkd=$(meson_multilib_native_use clipos_use_networkd)
		# CLIP OS: Disabled
		#-Dquotacheck=$(meson_multilib)
		-Dquotacheck=false
		-Drandomseed=$(meson_multilib)
		-Drfkill=$(meson_multilib)
		# CLIP OS: Enabled only for the SDK
		#-Dsysusers=$(meson_multilib)
		-Dsysusers=$(meson_multilib_native_use clipos_use_sysusers)
		# CLIP OS: Disable timedated support
		#-Dtimedated=$(meson_multilib)
		-Dtimedated=false
		# CLIP OS: Disable timesync support
		#-Dtimesyncd=$(meson_multilib)
		-Dtimesyncd=false
		-Dtmpfiles=$(meson_multilib)
		-Dvconsole=$(meson_multilib)

		# static-libs
		-Dstatic-libsystemd=$(usex static-libs true false)
		-Dstatic-libudev=$(usex static-libs true false)

		# CLIP OS: Always disabled
		-Dresolve=false
		-Dmachined=false
		-Dremote=false
		-Dnss-myhostname=false
		-Dhtml=false

		# CLIP OS: Specify this, or meson breaks due to no /etc/login.defs
		-Dsystem-gid-max=999
		-Dsystem-uid-max=999

		# CLIP OS: dbus paths
		-Ddbussessionservicedir="${EPREFIX}/usr/share/dbus-1/services"
		-Ddbussystemservicedir="${EPREFIX}/usr/share/dbus-1/system-services"

		# The CLIP OS epoch, Mon January 1 00:00:00 UTC 2018. Used by timesyncd
		# as a sanity check for the minimum acceptable time. Explicitly set
		# to avoid using the current build time.
		-Dtime-epoch=1514764800

		# CLIP OS: Set hostname fallback to original product shortname
		-Dfallback-hostname="clipos"

		# CLIP OS: Set the shell to spawn when the "early-root-shell"
		# instrumentation feature is enabled
		-Ddebug-shell=$(usex clipos_instrumentations_early-root-shell /bin/bash /sbin/nologin)
	)

	meson_src_configure "${myconf[@]}"
}

multilib_src_compile() {
	eninja
}

multilib_src_test() {
	unset DBUS_SESSION_BUS_ADDRESS XDG_RUNTIME_DIR
	meson_src_test
}

multilib_src_install() {
	DESTDIR="${D}" eninja install
}

multilib_src_install_all() {
	local rootprefix=$(usex split-usr '' /usr)

	# meson doesn't know about docdir
	mv "${ED}"/usr/share/doc/{systemd,${PF}} || die

	einstalldocs
	dodoc "${FILESDIR}"/nsswitch.conf

	if ! use resolvconf; then
		rm -f "${ED}${rootprefix}"/sbin/resolvconf || die
	fi

	rm "${ED}"/etc/init.d/README || die
	rm "${ED}${rootprefix}"/lib/systemd/system-generators/systemd-sysv-generator || die

	if ! use sysv-utils; then
		rm "${ED}${rootprefix}"/sbin/{halt,init,poweroff,reboot,runlevel,shutdown,telinit} || die
		rm "${ED}"/usr/share/man/man1/init.1 || die
		rm "${ED}"/usr/share/man/man8/{halt,poweroff,reboot,runlevel,shutdown,telinit}.8 || die
	fi

	if ! use resolvconf && ! use sysv-utils; then
		rmdir "${ED}${rootprefix}"/sbin || die
	fi

	# Preserve empty dirs in /etc & /var, bug #437008
	keepdir /etc/{binfmt.d,modules-load.d,tmpfiles.d}
	keepdir /etc/kernel/install.d
	keepdir /etc/systemd/{network,system,user}
	keepdir /etc/udev/rules.d

	if use hwdb; then
		keepdir /etc/udev/hwdb.d
	fi

	keepdir "${rootprefix}"/lib/systemd/{system-sleep,system-shutdown}
	keepdir /usr/lib/{binfmt.d,modules-load.d}
	keepdir /usr/lib/systemd/user-generators
	keepdir /var/lib/systemd
	keepdir /var/log/journal

	# Symlink /etc/sysctl.conf for easy migration.
	dosym ../sysctl.conf /etc/sysctl.d/99-sysctl.conf

	if use hwdb; then
		rm -r "${ED}${rootprefix}"/lib/udev/hwdb.d || die
	fi

	if use split-usr; then
		# Avoid breaking boot/reboot
		dosym ../../../lib/systemd/systemd /usr/lib/systemd/systemd
		dosym ../../../lib/systemd/systemd-shutdown /usr/lib/systemd/systemd-shutdown
	fi

	gen_usr_ldscript -a systemd udev

	# CLIP OS: Don't set any extra environment variables by default
	# rm "${ED%/}/usr/lib/environment.d/99-environment.conf" || die

	# CLIP OS: Remove all default configuration from /etc
	for d in \
		/etc/systemd/system \
		/etc/sysctl.d \
		/etc/sysctl.conf \
		/etc/tmpfiles.d; do
		rm -rf "${ED%/}"/${d}
	done

	# CLIP OS: Remove nspawn binary, tmpfiles & unit
	rm "${ED}"/usr/bin/systemd-nspawn
	rm "${ED}"/usr/lib64/tmpfiles.d/systemd-nspawn.conf
	rm "${ED}"/lib64/systemd/system/systemd-nspawn@.service

	# CLIP OS: Remove /sys/kernel/config & /sys/kernel/debug mounts
	rm "${ED}"/lib64/systemd/system/sys-kernel-config.mount
	rm "${ED}"/lib64/systemd/system/sys-kernel-debug.mount

	# CLIP OS: Remove /tmp mount
	rm "${ED}"/lib64/systemd/system/tmp.mount

	# CLIP OS: Do not enable random services if /etc was detected as empty!!!
	# rm "${ED%/}$(usex split-usr /usr '')/lib/systemd/system-preset/90-systemd.preset" || die
	insinto $(usex split-usr /usr '')/lib/systemd/system-preset
	doins "${FILESDIR}"/clipos/99-default.preset

	# CLIP OS: Do not ship distro-specific files (nsswitch.conf pam.d)
	rm -rf "${ED%/}"/usr/share/factory
	sed -i "${ED%/}"/usr/lib/tmpfiles.d/etc.conf \
		-e '/^C \/etc\/nsswitch\.conf/d' \
		-e '/^C \/etc\/pam\.d/d'

	# CLIP OS: Replace default sysusers configs with fixed UID:GID versions
	insinto /usr/lib/sysusers.d
	doins "${FILESDIR}"/clipos/basic.conf
	doins "${FILESDIR}"/clipos/systemd.conf

	# CLIP OS: Install systemd-journald config override
	insinto /etc/systemd/journald.conf.d/
	doins "${FILESDIR}"/clipos/journald/default.conf
}

migrate_locale() {
	local envd_locale_def="${EROOT}/etc/env.d/02locale"
	local envd_locale=( "${EROOT}"/etc/env.d/??locale )
	local locale_conf="${EROOT}/etc/locale.conf"

	if [[ ! -L ${locale_conf} && ! -e ${locale_conf} ]]; then
		# If locale.conf does not exist...
		if [[ -e ${envd_locale} ]]; then
			# ...either copy env.d/??locale if there's one
			ebegin "Moving ${envd_locale} to ${locale_conf}"
			mv "${envd_locale}" "${locale_conf}"
			eend ${?} || FAIL=1
		else
			# ...or create a dummy default
			ebegin "Creating ${locale_conf}"
			cat > "${locale_conf}" <<-EOF
				# This file has been created by the sys-apps/systemd ebuild.
				# See locale.conf(5) and localectl(1).

				# LANG=${LANG}
			EOF
			eend ${?} || FAIL=1
		fi
	fi

	if [[ ! -L ${envd_locale} ]]; then
		# now, if env.d/??locale is not a symlink (to locale.conf)...
		if [[ -e ${envd_locale} ]]; then
			# ...warn the user that he has duplicate locale settings
			ewarn
			ewarn "To ensure consistent behavior, you should replace ${envd_locale}"
			ewarn "with a symlink to ${locale_conf}. Please migrate your settings"
			ewarn "and create the symlink with the following command:"
			ewarn "ln -s -n -f ../locale.conf ${envd_locale}"
			ewarn
		else
			# ...or just create the symlink if there's nothing here
			ebegin "Creating ${envd_locale_def} -> ../locale.conf symlink"
			ln -n -s ../locale.conf "${envd_locale_def}"
			eend ${?} || FAIL=1
		fi
	fi
}

save_enabled_units() {
	ENABLED_UNITS=()
	type systemctl &>/dev/null || return
	for x; do
		if systemctl --quiet --root="${ROOT:-/}" is-enabled "${x}"; then
			ENABLED_UNITS+=( "${x}" )
		fi
	done
}

pkg_preinst() {
	save_enabled_units {machines,remote-{cryptsetup,fs}}.target getty@tty1.service

	if ! use split-usr; then
		local dir
		for dir in bin sbin lib; do
			if [[ ! ${EROOT}/${dir} -ef ${EROOT}/usr/${dir} ]]; then
				eerror "\"${EROOT}/${dir}\" and \"${EROOT}/usr/${dir}\" are not merged."
				eerror "One of them should be a symbolic link to the other one."
				FAIL=1
			fi
		done
		if [[ ${FAIL} ]]; then
			eerror "Migration to system layout with merged directories must be performed before"
			eerror "rebuilding ${CATEGORY}/${PN} with USE=\"-split-usr\" to avoid run-time breakage."
			die "System layout with split directories still used"
		fi
	fi
}

pkg_postinst() {
	systemd_update_catalog

	# Keep this here in case the database format changes so it gets updated
	# when required. Despite that this file is owned by sys-apps/hwids.
	if has_version "sys-apps/hwids[udev]"; then
		udevadm hwdb --update --root="${EROOT}"
	fi

	udev_reload || FAIL=1

	# Bug 465468, make sure locales are respect, and ensure consistency
	# between OpenRC & systemd
	migrate_locale

	# CLIP OS: Required services are re-enabled in the configure step
	# systemd_reenable systemd-networkd.service systemd-resolved.service

	if [[ ${ENABLED_UNITS[@]} ]]; then
		systemctl --root="${ROOT:-/}" enable "${ENABLED_UNITS[@]}"
	fi

	if [[ -z ${REPLACING_VERSIONS} ]]; then
		if type systemctl &>/dev/null; then
			systemctl --root="${ROOT:-/}" enable getty@.service remote-fs.target || FAIL=1
		fi
		elog "To enable a useful set of services, run the following:"
		elog "  systemctl preset-all --preset-mode=enable-only"
	fi

	if [[ -L ${EROOT}/var/lib/systemd/timesync ]]; then
		rm "${EROOT}/var/lib/systemd/timesync"
	fi

	if [[ -z ${ROOT} && -d /run/systemd/system ]]; then
		ebegin "Reexecuting system manager"
		systemctl daemon-reexec
		eend $?
	fi

	if [[ ${FAIL} ]]; then
		eerror "One of the postinst commands failed. Please check the postinst output"
		eerror "for errors. You may need to clean up your system and/or try installing"
		eerror "systemd again."
		eerror
	fi
}

pkg_prerm() {
	# If removing systemd completely, remove the catalog database.
	if [[ ! ${REPLACED_BY_VERSION} ]]; then
		rm -f -v "${EROOT}"/var/lib/systemd/catalog/database
	fi
}

# vim: set ts=8 sts=8 sw=8 ai noet:
